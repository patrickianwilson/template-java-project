buildscript {
    repositories {
        maven {
            url "https://maven.pkg.jetbrains.space/inquest/p/buildtools/gradle-plugins-internal"
            credentials {
                username = spaceUsername
                password = spacePassword
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.inquestdevops:WarblerBuildGradlePlugin:1.0.+"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.15"
        classpath "io.freefair.gradle:lombok-plugin:6.4.1"
    }
}

project.version = 'dev.1'
project.group = "com.github.patrickianwilson"

if (System.getenv("BUILD_NUMBER") != null) {
    def majorVersion = "1.0" //eventually override with a lookup
    def buildStream = "0"
    project.version = "${majorVersion}.${buildStream}.${System.getenv("BUILD_NUMBER")}"
}

ext {
    moduleName = "%%{{ModuleName}}%%" //change me!!
}


apply plugin: "project.java"
repositories {
    maven {
        url "https://maven.pkg.jetbrains.space/inquest/p/buildtools/build-tools"
        credentials {
            username = spaceUsername
            password = spacePassword
        }
    }
}

dependencies {
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java',
            version: '4.11.0'
    // https://mvnrepository.com/artifact/io.cucumber/cucumber-java
    testImplementation group: 'io.cucumber', name: 'cucumber-java', version: '6.10.4'
    // https://mvnrepository.com/artifact/io.cucumber/cucumber-junit
    testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '6.10.4'
    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    //needed to select the correct Rabbit environments for this test.
    testImplementation "com.github.patrickianwilson:RabbitServiceJavaApi:1.0.0.+"
    implementation "ch.qos.logback:logback-core:1.1.3"
    implementation "ch.qos.logback:logback-classic:1.1.3"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'com.inquestdevops.sandstorm.cucumber.features',
                    'src/test/features']
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "${project.ext.moduleName}"
            from components.java
        }
    }
    repositories {
        maven {
            name = 'mavenInternal'
            url "https://maven.pkg.jetbrains.space/inquest/p/buildtools/build-tools"
            credentials {
                username = spaceUsername
                password = spacePassword
            }
        }
    }
}
tasks.test.dependsOn "cucumberCli"

tasks.release.dependsOn "validate"
if (project.version != "dev.1") {
    project.tasks.release.dependsOn("publishAllPublicationsToMavenInternalRepository")
}
